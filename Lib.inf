!! This file contains common classes and procedures.
! Rooms
Class   ScoredRoom
 class  Room
 with   before [ way;
         Go:
            way = location.(noun.door_dir)();
            if (way ofclass Sea) {
                if (~~(location ofclass RideableObject)) {
                    print "You're not a swimmer.";
                    return 2;
                } else if (player in raft) {
                    print "You push off carefully from shore onto the lake,
                        feeling small in your ramshackle raft.";
                    return 1;
                }
            } else if (location ofclass Sea) {
                print "You push onto shore and get off your raft.";
                move player to self;
                return 1;
            } else return 0;
        ],
 has    scored;

Class   WindowRoom
 class  ScoredRoom
 with   description "A featureless, empty room, apart from a small
            north window that lets light flood in.",
        window_dir n_obj,
        compass_look [obj;
            if (obj == window_dir)
                print_ret (string) location.(obj.door_dir).description;
        ],
 has    light;

Class   Exterior
 class  ScoredRoom
 with   description "A grassy meadow.",
        compass_look [obj;
            if (obj ~= d_obj && obj ~= u_obj)
                print_ret "In that direction, ", (string)
                    location.(obj.door_dir).description;
            else rfalse;
        ];

Class   RideableObject
 has    enterable;

Class   Sea
 class  Exterior
 with   description [;
            "", (The) child(self), " stretches on into the distance,
                shining blue, sparkling in the sunlight.";
        ];

! NPCs
! To activate random move, do:
! StartDaemon(usher);
! PC_Path(usher, RANDOM_MOVE|AIMED_MOVE|PRESET_MOVE, 80);
Class   NPC
 class  MoveClass
 with   description "A rather nondescript looking townsperson.",
        walkon "shows up",
        walkoff "walks away",
 has    animate male;

[ DescribeTameness x;
    switch (x) {
     0: return "very wary";
     1: return "a little wary";
     2: return "pretty shy";
     3: return "a little shy";
     4: return "cautiously freindly";
     5: return "freindly";
     6: return "a bit trusting";
     7: return "pretty trusting";
     8: return "trusting";
     9: return "very trusting";
     10: return "tame";
     11: return "very tame";
     12: return "extremely tame";
     default: return "loving";
    }
];

Class   RideableNPC
 class  NPC
 with   tamed false,
        tamelevel 0,
        initial [;
            NPC_Path(self, RANDOM_MOVE, 80);
            "", (A) self, " stands here placidly."; ],
        life [;
         Order: print_ret (The) self, " looks confused.";
         Tell, Ask, Answer:
            self.tamelevel = self.tamelevel + 2;
            print (The) self, " seems ",
                (string) DescribeTameness(self.tamelevel), ".";
            if (self.tamelevel >= 10) self.tamed = true;
            rtrue;
         Kiss:
            <<Sing>>;
        ],
        before [;
         Go:
            if (self.tamed)
                return 1;
            else {
                print "The horse refuses to budge.";
                return 2;
            }
            
         Climb: <<Enter self>>;
         Enter:
            if (self.tamed) {
                print "You climb onto the ", (the) self, ".";
                move player to self;
                rtrue;
            } else {
                player.health--;
                print "You climb onto ", (the) self, ", but it throws
                    you off!";
                StartDaemon(self);
                style bold;
                print "^[You are now ",
                    (string) DescribeStat(player.health), "
                    healthy.]";
                style roman;
                rtrue;
            }
            
         Receive:
            if (noun == player) {
                <<Enter self>>;
            } else if (noun has edible) {
                remove noun;
                print_ret (The) self, " voraciously eats the ", (the)
                 noun, ".^";
            } else {
                print_ret "It wouldn't know what to do with that.^";
            }
            
         Touch, Rub, Show:
            StopDaemon(self);
            self.tamelevel++;
            print (The) self, " seems ",
                (string) DescribeTameness(self.tamelevel), ".";
            if (self.tamelevel >= 10) self.tamed = true;
            rtrue;
            
         Jump, Mild, WakeOther, Tie, JumpOver:
            self.tamelevel--;
            print_ret (The) self, " is startled. After awhlie, it seems to
                ", (string) DescribeTameness(self.tamelevel), ".";
            
         Cut, Burn, Dig, Squeeze, Strong, Dig, Blow, Attack, Swing,
         ThrowAt, Push, Pull, PushDir:
            self.tamelevel = 0;
            StartDaemon(self);
            print_ret (The) self, " is extremely startled! It loses
                its trust in you.";
        ],
        react_before [;
         Sing:
            StopDaemon(self);
            self.tamelevel = self.tamelevel + 3;
            print (The) self, " seems ",
                (string) DescribeTameness(self.tamelevel), ".";
            if (self.tamelevel >= 10) self.tamed = true;
            rtrue;
         Take:
            if (real_location == self)
                "You'll have to get off ", (the) self, " first. You don't
                    have arms that long.";
        ],
        react_after [;
         Exit,GetOff:
            print (The) self, " looks at you.";
            rfalse;
        ],
 has    open enterable transparent;

Class   CatchableNPC
 class  NPC
 with   walkon "circles in",
        walkoff "flutters off",
        before [;
         Take:
            if (player.fatigue < 5) {
                move self to player;
                "Lighteningly quick, you lash out your hand and catch
                ", (the) self, "!";
            } else "Try as you might, you can't quite catch ", (the)
             self, ".";
        ],
 has    neuter;

! Tools
Class   Tool;

Class   SharpTool
 class  tool
 with   sharpness;

Class   Weapon
 class  SharpTool
 with   attack;

! Fixtures
Class   Scenic
 has    scenery;

Class   Landmark
 with   description "A rather tall peice of rock.",
        found_in [; return location ofclass Exterior; ],
 has    scenery;

! Make choppable in fractions like Liquid
Class   Plant
 class  Scenic
 with   description "A leafy grean plant.",
        tall false,
        rigid false,
        chopped 4,
        felled false,
        before [;
         Attack, Cut:
            self.chopped--;
            if (self.chopped > 0) {
                move Wood.create() to parent(self);
                "You chop at the tree, in the process cutting a
                branch off as well.";
            }
            else "You chop most of the way through the tree,
            leaving a tiny peice to hold it up until you push it over.";
         Push: "What directon would you push it?";
         PushDir:
            if (second == n_obj && self.chopped == 0) {
                self.felled = true;
                "", (The) self, " falls over slowly, making loud
                    cracking noises.";
            } else {
                if (self.chopped == 4)
                    "You'll need to actually chop that ", (name) self, ".";
                else if (self.chopped >= 2)
                    "You'll need to chop that ", (name) self, " a couple more times.";
                else if (self.chopped == 1)
                    "One more swing!";
            };
            rtrue;
         PutOn:
            if (self.tall) {
                if (real_location == self) {
                    print "You carefully place ", (the) noun, " on
                        a branch.";
                    rfalse;
                } else {
                    print "You can't reach far enough up to put that
                        in the ", (the) self, ". Maybe try climbing?";
                    rtrue;
                }
            } else if (self.rigid) rfalse;
            else rtrue;
         Climb: <<Enter self>>;
         Enter:
            if (self.rigid) {
                print "You climb ", (the) self, ".";
                move player to self;
            } else {
                print "You would crush it!";
            }
            rtrue;
         GetOff:
            if (self.tall) {
                print "You carefully jump off, landing on the
                    ground.";
            } else {
                print "You hop down.";
            }
            move player to parent(self);
        ],
 has    enterable supporter transparent;

Class   Wood(10)
 with   name 'branch' 'wood' 'log',
        short_name "wood",
        description "A simple branch, reasonably thick and wide.";
